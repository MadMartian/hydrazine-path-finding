repositories {
    mavenLocal()
    maven {
        name = "central"
        url = "https://repo1.maven.org/maven2/"
    }
    maven { url 'https://jitpack.io' }
}

group 'com.extollit.gaming'
description = "An iterative A* path-finding engine designed for blocky sandbox games having entities with differing path requirements"
version '1.7.2'

apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

sourceSets {
    example {
        java {
            compileClasspath = main.compileClasspath + main.output
            runtimeClasspath = main.runtimeClasspath + main.output
        }
    }
    test {
        java {
            compileClasspath = test.compileClasspath + example.output
            runtimeClasspath = test.runtimeClasspath + example.output
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: "org.mockito", name: "mockito-core", version: "1.10.19"

    compile group: 'com.extollit', name: 'data-structures', version: '2.18'
}

task shipDoc(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task shipSrc(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives shipSrc
    archives shipDoc
}

if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword") &&
    project.hasProperty("signing.keyId") && project.hasProperty("signing.password") && project.hasProperty("signing.secretKeyRingFile"))
    apply from: 'publishing.gradle'
