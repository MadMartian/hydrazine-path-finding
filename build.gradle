repositories {
    mavenLocal()
    maven {
        name = "central"
        url = "https://repo1.maven.org/maven2/"
    }
    maven { url 'https://jitpack.io' }
}

group 'com.extollit.gaming'
description = "An iterative A* path-finding engine designed for blocky sandbox games having entities with differing path requirements"
version '1.6.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

sourceSets {
    example {
        java {
            compileClasspath = main.compileClasspath + main.output
            runtimeClasspath = main.runtimeClasspath + main.output
        }
    }
    test {
        java {
            compileClasspath = test.compileClasspath + example.output
            runtimeClasspath = test.runtimeClasspath + example.output
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: "org.mockito", name: "mockito-core", version: "1.10.19"

    compile group: 'com.extollit', name: 'data-structures', version: '2.14'
}

signing {
    sign configurations.archives
}

task shipDoc(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task shipSrc(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives shipSrc
    archives shipDoc
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'RabbitMQ Manager'
                packaging 'jar'
                description 'Convenience class for facilitating some common RabbitMQ tasks.'
                url 'https://github.com/weibeld/RabbitMQ-Manager'

                scm {
                    connection 'scm:git:git://github.com/weibeld/RabbitMQ-Manager.git'
                    developerConnection 'scm:git:ssh://github.com/weibeld/RabbitMQ-Manager.git'
                    url 'http://github.com/weibeld/RabbitMQ-Manager/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'MadMartian'
                        name 'Jonathan Neufeld'
                        email 'support@extollit.com'
                        organization 'extollIT Enterprises'
                        organizationUrl 'http://www.extollit.com'
                    }
                }
            }
        }
    }
}